buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://plugins.gradle.org/m2' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = '1.12.2-v1.1'
group = 'bl4ckscor3.mod.particleculling'
archivesBaseName = 'particleculling'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run/client'
    coreMod = 'bl4ckscor3.mod.particleculling.ParticleCullingLoadingPlugin'
    mappings = 'stable_39'
}

jar.manifest.attributes(
        'FMLCorePluginContainsFMLMod': true,
        'FMLCorePlugin': 'bl4ckscor3.mod.particleculling.ParticleCullingLoadingPlugin',
        'ForceLoadAsMod': true,
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'FMLAT': 'particleculling_at.cfg'
)

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven {	url = 'https://cursemaven.com/' }
}

dependencies {
    compile 'org.spongepowered:mixin:0.8.1-SNAPSHOT'
    deobfCompile "curse.maven:dynamic-surroundings-238891:3098835"
    deobfCompile "curse.maven:orelib-307806:2820815"
}

mixin {
    add sourceSets.main, 'mixins.particleculling.refmap.json'
}

shadowJar {
    exclude 'dummyThing'
    classifier = ''
}
build.dependsOn(shadowJar)

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
